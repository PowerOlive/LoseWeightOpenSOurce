apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.allever.app.lose.weight.opensource"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        //在Manifest.xml中使用的值
        manifestPlaceholders += [
                UMENG_APP_KEY: rootProject.ext.umengAppKey
        ]

        //解决D8: Resource 'META-INF/MANIFEST.MF' already exists.
        packagingOptions {
            exclude 'META-INF/library_release.kotlin_module'
        }

    }

    //解决butterknife编译异常
    //Failed to transform artifact 'butterknife-runtime.aar (com.jakewharton:butterknife-runtime:10.1.0)' to match attributes {artifactType=android-dex, dexing-is-debuggable=true, dexing-min-sdk=19}
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    //友盟多渠道
    flavorDimensions "mark"
    productFlavors {
        google {}
//        xiaomi {}
//        baidu {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    signingConfigs {
        release {
            storeFile file("${rootProject.ext.signingStoreFile}")
            storePassword "${rootProject.ext.signingStorePassword}"
            keyAlias "${rootProject.ext.signingKeyAlias}"
            keyPassword "${rootProject.ext.signingKeyPassword}"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all {
                variant ->
                    if (variant.buildType.name.equals('release')) {
                        variant.outputs.each { output ->
                            def outputFile = output.outputFile
                            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                                def fileName = "${defaultConfig.applicationId}_(Release)_V${defaultConfig.versionName}_C${defaultConfig.versionCode}_${variant.flavorName}_(Build${releaseTime()}).apk"
                                output.outputFileName = new File(fileName)
                                //com.allever.security.photo.browser_(Release)_V1.0_C1_(Build201908251506).apk
                            }
                        }
                    }
            }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("Asia/Chongqing"))
}

//引入aar包需加
repositories {
    flatDir {
        dirs 'libs'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation project(':common')
    implementation project(':umeng')
    implementation project(':recommend')
    implementation(name: "Toolkit-1.1", ext: 'aar')

// 如果想使用SwipeBack 滑动边缘退出Fragment/Activity功能，完整的添加规则如下：
    implementation 'me.yokeyword:fragmentationx:1.0.2'
// swipeback基于fragmentation, 如果是自定制SupportActivity/Fragment，则参照SwipeBackActivity/Fragment实现即可
    implementation 'me.yokeyword:fragmentationx-swipeback:1.0.2'

// Activity作用域的EventBus，更安全，可有效避免after onSavenInstanceState()异常
    implementation 'me.yokeyword:eventbus-activity-scope:1.1.0'
// Your EventBus's version
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'

    implementation 'com.dinuscxj:circleprogressbar:1.1.1'
    //图表控件
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    //日历控件
    implementation 'com.haibin:calendarview:3.2.6'
    implementation 'com.google.code.gson:gson:2.8.0'
    //Google Fit
    implementation 'com.google.android.gms:play-services-fitness:18.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    //消息事件
    implementation 'org.greenrobot:eventbus:3.1.1'
    //数据库
    implementation 'org.litepal.android:core:1.6.1'
    //Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

}
repositories {
    mavenCentral()
}
